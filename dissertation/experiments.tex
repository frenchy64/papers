\chapter{Experiments}

In this chapter we work through several examples of using Typed Clojure and
gauge how well the current prototype handles them. 
We intentionally chose examples that could be challenging to type check,
or were particularly useful to the everyday Clojure programmer.

\section{Java Interoperability}
\label{sec:experimentinterop}

As Typed Clojure is intended to be useful for practical purposes, it was important
to port existing code that utilized Java interoperability.
My porting effort attempted to follow how a real programmer might port code to Typed Clojure
and I describe my approach for each situation.

I ported a function from clojure.contrib.reflect\footnote{https://github.com/richhickey/clojure-contrib},
a Clojure library relying heavily on Java interoperability.
I chose \lstinline|call-method| for several reasons: it chains several Java calls together,
it uses primitive arrays, it exposes a wart of Clojure with \emph{Named} things,
and \lstinline|null| is a valid value in one place.

Before showing the implementation, there is a brief explanation of the relevant syntax.
Java methods are called using the \emph{dot} operator. If \lstinline|o| is an object \lstinline|(. o m a*)|
calls its method named \lstinline|m| passing \lstinline|a*| arguments. The method can be named first
using the equivalent sugar \lstinline|(.m o a*)|. Also, \lstinline|doto| is convenient 
notation for muliple method calls on the same object, presumably for side effects, and returns the original
object. For example, \lstinline|(doto o (.m1 a*) (m2 a*))| calls methods \lstinline|m1| and \lstinline|m2|
on \lstinline|o| and returns \lstinline|o|.

\begin{lstlisting}[caption=call-method, label=lst:callmethod]
;; call-method pulled from clojure.contrib.reflect, (c) 2010 Stuart Halloway & Contributors
(defn call-method
  "Calls a private or protected method.

  params is a vector of classes which correspond to the arguments to
  the method e

  obj is nil for static methods, the instance object otherwise.

  The method-name is given a symbol or a keyword (something Named)."
  [^Class klass method-name params obj & args]
  (let [method (doto (.getDeclaredMethod klass 
                                         (name method-name)
                                         (into-array Class params))
                 (.setAccessible true))]
    (.invoke method obj (into-array Object args))))
\end{lstlisting}

The original function is modified slightly for readability and is presented in Listing \ref{lst:callmethod}.

\begin{lstlisting}[caption=call-method Type Annotation, label=lst:callmethodtype]
(ann call-method 
     [Class Named (IPersistentVector Class) (U nil Object) (U nil Object) * -> (U nil Object)])
\end{lstlisting}

Thankfully this function has up-to-date documentation, and from it we can derive an expected type
(Listing \ref{lst:callmethodtype}).

Before running the type checker we must convert our array constructors into ones that Typed Clojure
can understand. Array types in Typed Clojure are represented by \lstinline|(Array c t)|, where
the Java class \lstinline|c| is the Java component type,
and the Typed Clojure type \lstinline|t| is the Typed Clojure component type. 
We can pass this array to Java methods accepting
type \lstinline|c[]|, and we can read and write type \lstinline|t| to the array from Typed Clojure.

\begin{itemize}
\item \lstinline|(into-array Class params)| becomes \lstinline|(into-array> Class Class params)|
      which is of type \lstinline|(Array Class Class)|.
\item \lstinline|(into-array Object args)| becomes \lstinline|(into-array> Object (U nil Object) args)|
      which is of type \lstinline|(Array Object (U nil Object))|.
\end{itemize}

The second place in the \lstinline|Array| type constructor allows fine grained control over what is allowed
in the array. The first point above must be type \lstinline|(Array Class Class)| because the 
\lstinline|getDeclaredMethod| method on \lstinline|java.lang.Class| instances requires an array of non-null
\lstinline|Class| objects. On the other hand, the \lstinline|invoke| method takes an array that allows
\emph{null} members, so its type is \lstinline|(Array Object (U nil Object))|.

Now we run the type checker, which produces a type error.

\begin{lstlisting}
#<Exception java.lang.Exception: 29: Cannot call instance method java.lang.reflect.AccessibleObject/setAccessible on type (U nil java.lang.reflect.Method)>
\end{lstlisting}

Because Typed Clojure assumes all methods return nilable Objects, the call to \lstinline|getDeclaredMethod|
has return type \lstinline|(U nil java.lang.reflect.Method)|. It is not type safe to call \lstinline|setAccessible|
on this type, so we get a type error.

In this case, Typed Clojure is too conservative: according to its documentation \lstinline|getDeclaredMethod|
never returns \emph{null}. We add this rule with \lstinline|non-nil-return|.

\begin{lstlisting}
(non-nil-return java.lang.Class/getDeclaredMethod :all)
\end{lstlisting}

Running the type checker produces a different type error.

\begin{lstlisting}
#<Exception java.lang.Exception: Type Error, REPL:32 - (U java.lang.Object nil) is not a subtype of: java.lang.Object>
\end{lstlisting}

This concerns passing \lstinline|obj| as the first argument to the \lstinline|invoke| method.
Typed Clojure conservatively defaults method parameter types as non-nullable. 
Therefore the first parameter of \lstinline|invoke| is \lstinline|Object| by default; \lstinline|obj|
is \lstinline|(U java.lang.Object nil)|. Again, this is too conservative as the first argument can
be \emph{null} for static methods, and we use \lstinline|nilable-param| to specify the first argument
of \lstinline|invoke| may be nil, for the arity of two parameters.

\begin{lstlisting}
(nilable-param java.lang.reflect.Method/invoke {2 #{0}})
\end{lstlisting}

The final successfully type checked code is presented in Listing \ref{lst:callmethodfinal}.

\begin{lstlisting}[caption=Type Annotated code for call-method, label=lst:callmethodfinal]
(non-nil-return java.lang.Class/getDeclaredMethod :all)
(nilable-param java.lang.reflect.Method/invoke {2 #{0}})

(ann call-method [Class Named (IPersistentVector Class) (U nil Object) (U nil Object) * -> (U nil Object)])

;; call-method pulled from clojure.contrib.reflect, (c) 2010 Stuart Halloway & Contributors
(defn call-method
  "Calls a private or protected method.

  params is a vector of classes which correspond to the arguments to
  the method e

  obj is nil for static methods, the instance object otherwise.

  The method-name is given a symbol or a keyword (something Named)."
  [^Class klass method-name params obj & args]
  (let [method (doto (.getDeclaredMethod klass 
                                         (name method-name)
                                         (into-array> Class Class params))
                 (.setAccessible true))]
    (.invoke method obj (into-array> Object (U nil Object) args))))
\end{lstlisting}

\section{Red-black trees}

This experiment involved porting an implementation of red-black trees
used as an experiment for SML CIDRE by Davies~\cite{Dav05}.
SML CIDRE is a sort-checker for Standard ML that supports refinement
types.
The red-black tree implementation was ported to work with SML CIDRE
to statically check red-black tree invariants.
It is a particularly good experiment for SML CIDRE because it generates an unusually large
amount of intersection types.

Intersection types in SML CIDRE are heavily optimised via memoisation.
Because of this, SML CIDRE is able to check this experiment with little trouble.
The current Typed Clojure prototype is not as successful, and appears to hang
during type checking.
The reasons are not entirely clear, but it is likely the number of intersection types
are too large for the current prototype to handle, which has no optimisations
around intersection types.

This experiment was intended to stress test Typed Clojure to an extreme, and
it is unsurprising that Typed Clojure was not up to the task.
One interesting point was noticed when porting it.
The SML CIDRE version of the red-black tree experiment
uses SML CIDRE datasorts to represent the red-black tree invariants.
The Typed Clojure version entirely uses plain hash-maps, a common Clojure idiom.
It was noticed that the heterogeneous map types that Typed Clojure provides would be sufficient to
represent the red-black tree invariants.

\section{Monads}
\label{sec:monads}

Monads are an interesting control structure used in functional programming languages,
as described by Wadler~\cite{Wad95}.
They are most recognisable from its inclusion in the statically-typed language Haskell~\cite{Mar10},
where monads are relied on for many features including global state, file output, and exceptions.

I chose to port the Clojure Contrib library \emph{algo.monads}~\cite{Hic12} to Typed Clojure. The library
provides several kinds of monads, monad transformers, and monadic functions.
Macros are used to provide pleasant syntax for consumers of the library.

\subsection{Monad Definitions}
\label{sec:monaddef}

This library represents a monad as a hash-map with four keys: \lstinline|:m-bind|, \lstinline|:m-result|,
\lstinline|:m-zero|, and \lstinline|:m-plus|. A valid monad must provide the first two, and the latter
two may optionally be mapped to the keyword \lstinline|::undefined|\footnote{Keywords prefixed with \lstinline|::|
are qualified in the current namespace.}.

\begin{lstlisting}[caption=Untyped definition for the identity monad, label=lst:identitymdef]
(defmonad identity-m
   "Monad describing plain computations. This monad does in fact nothing
    at all. It is useful for testing, for combination with monad
    transformers, and for code that is parameterized with a monad."
  [m-result identity
   m-bind   (fn m-result-id [mv f]
              (f mv))
  ])
\end{lstlisting}

A monad is defined using the macro \lstinline|defmonad|. The macro expands to code that binds a var to a hash-map
with the aforementioned keys.

\begin{lstlisting}[caption=Type for identity monad, label=lst:identitymtype]
(def-alias Undefined '::undefined)
(ann identity-m
     '{:m-bind (All [x y]
                 [x [x -> y] -> y])
       :m-result (All [x]
                   [x -> x])
       :m-zero Undefined
       :m-plus Undefined})
\end{lstlisting}

A direct typing of the identity monad would look like Listing \ref{lst:identitymtype}.
This signature assigns the expected type for the var \lstinline|identity-m| 
to be a heterogeneous map type with the minimum entries for monads: \lstinline|:m-bind|
and \lstinline|:m-result|. The syntax for heterogeneous map types requires a \lstinline|'| prefix,
and any number of key-value pairs are given between curly braces. Heterogeneous map keys must be keywords,
so the syntax is made more convenient by omitting the usually required \lstinline|'| prefix
for keyword types.

Monad types in languages with advanced type systems are often abstracted using type constructors.
This allows reasoning about monadic code while keeping the particular monad abstract, which is a desirable result,
so Typed Clojure was extended to support user definable type constructors.

\begin{lstlisting}[caption=An abstract definition of a monad., label=lst:monadalias]
(def-alias Monad 
  (TFn [[m :kind (TFn [[x :variance :covariant]] Any)]]
    '{:m-bind (All [x y]
                [(m x) [x -> (m y)] -> (m y)])
      :m-result (All [x]
                  [x -> (m x)])
      :m-zero Undefined
      :m-plus Undefined}))
\end{lstlisting}

Listing \ref{lst:monadalias} captures the abstract definition of a monad.
A \lstinline|Monad| is a type constructor parameterised by \lstinline|m|, which is
a type constructor taking a single argument \lstinline|x| (a type) and returning a type 
(written \lstinline|Any|)\footnote{Haskell-like syntax is helpful here, the kind \lstinline|(TFn [[x :variance :covariant]] Any)|
is approximately \lstinline|* -> *|. A more streamlined syntax is planned for future work.}.
The body of the type constructor uses \lstinline|m| abstractly. Typed Clojure
ensures the correct number of arguments are passed and recognises the declared variances
for each parameter, In this case the first argument of \lstinline|m| is declared
a covariant position. When instantiated, \lstinline|m| must also be a type operator
of one covariant argument returning a type.

\begin{lstlisting}[caption=Identity monad using user defined type constructors, label=lst:identityctors]
(ann identity-m (Monad (TFn [[x :variance :covariant]] x)))
\end{lstlisting}

We can now express the type of the identity monad more abstractly (Listing \ref{lst:identityctors}).
The purpose of the monad seems more apparent just from reading its type.
In this case, the fact that identity monad has no effect is reflected
by its type constructor returning exactly its argument.

\begin{lstlisting}[caption=Type checked identity monad definition, label=lst:identitydeftyped]
(defmonad identity-m
   "Monad describing plain computations. This monad does in fact nothing
    at all. It is useful for testing, for combination with monad
    transformers, and for code that is parameterized with a monad."
  [m-result identity
   m-bind   (ann-form
              (fn m-result-id [mv f]
                (f mv))
              (All [x y]
                [x [x -> y] -> y]))
  ])
\end{lstlisting}

The final type checked definition for the identity monad is given in Listing \ref{lst:identitydeftyped}.
In this case, just the monadic bind required annotation.

\begin{lstlisting}[caption=Several monad types, label=lst:severalmonads]
; Maybe monad
(ann maybe-m (MonadPlusZero
               (TFn [[x :variance :covariant]] 
                 (U nil x))))

; Sequence monad (called "list monad" in Haskell)
(ann sequence-m (MonadPlusZero 
                  (TFn [[x :variance :covariant]]
                    (Seqable x))))

; State monad
(def-alias State
  (TFn [[r :variance :covariant]
        [s :variance :invariant]]
    [s -> '[r s]]))
(ann state-m (All [s]
               (Monad (TFn [[x :variance :covariant]]
                        (State x s)))))
\end{lstlisting}

Listing \ref{lst:severalmonads} shows several monad types. The alias \lstinline|MonadPlusZero|
is identical to \lstinline|Monad|, except both \lstinline|:m-zero| and \lstinline|:m-plus| are defined.
The definitions were type checked by adding type annotations in appropriate places, in a similar fashion
to the identity monad. A significant obstacle was discovered while type checking monad definitions,
related to filtering sequences with negative type information (discussed in Section \ref{sec:filterneg}).

\subsection{Monad Transformer Definitions}

The initial motivation for adding user defined type constructors to Typed Clojure
(as discussed in Section \ref{sec:monaddef}) was to type check
monad transformer definitions.
Monad transformers are always parameterised by a monad type constructor, and should work for all
monad type constructors. Keeping the monad type constructor abstract allows us to determine
whether a monad transformer works \emph{for all} monads.

Listing \ref{lst:maybet} shows the full code for type checking the definition
of the maybe monad transformer.
Typing this example was a matter of assigning types to anonymous functions
(similar to standard monad definitions) and manually instantiating 
polymorphic types with \lstinline|inst|
that parameterise over higher kinds, which cannot be inferred by Typed Clojure.

\begin{lstlisting}[caption={Maybe monad transformer in Typed Clojure}, label=lst:maybet]
(ann maybe-t
     (All [[m :kind (TFn [[x :variance :covariant]] Any)]]
       (Fn 
         [(AnyMonad m) -> (MonadPlusZero (TFn [[y :variance :covariant]]
                                           (m (U nil y))))]
         [(AnyMonad m) nil -> (MonadPlusZero (TFn [[y :variance :covariant]]
                                               (m (U nil y))))]
         [(AnyMonad m) nil (U ':m-plus-default ':m-plus-from-base)
          -> (MonadPlusZero (TFn [[y :variance :covariant]]
                              (m (U nil y))))])))
(defn maybe-t
  "Monad transformer that transforms a monad m into a monad in which
   the base values can be invalid (represented by nothing, which defaults
   to nil). The third argument chooses if m-zero and m-plus are inherited
   from the base monad (use :m-plus-from-base) or adopt maybe-like
   behaviour (use :m-plus-from-transformer). The default is :m-plus-from-base
   if the base monad m has a definition for m-plus, and
   :m-plus-from-transformer otherwise."
  ([m] ((inst maybe-t m) m nil :m-plus-default))
  ([m nothing] ((inst maybe-t m) m nothing :m-plus-default))
  ([m nothing which-m-plus]
   (monad-transformer m which-m-plus
     [m-result (with-monad m m-result)
      m-bind   (with-monad m
                 (ann-form
                   (fn m-bind-maybe-t [mv f]
                     (m-bind
                       mv
                       (ann-form
                         (fn [x]
                           (if (nil? x)
                             (m-result nothing) 
                             (f x)))
                         [(U nil a) -> (m (U nil b))])))
                   (All [a b]
                     [(m (U nil a)) [a -> (m (U nil b))] -> (m (U nil b))])))
      m-zero   (with-monad m (m-result nothing))
      m-plus   (with-monad m
                 (ann-form
                   (fn m-plus-maybe-t [& mvs]
                     (if (empty? mvs)
                       (m-result nothing)
                       (m-bind (first mvs)
                               (ann-form
                                 (fn [v]
                                   (if (= v nothing)
                                     (apply m-plus-maybe-t (rest mvs))
                                     (m-result v)))
                                 [(U x nil) -> (m (U x nil))]))))
                   (All [x] [(m (U x nil)) * -> (m (U x nil))])))
      ])))

\end{lstlisting}

\section{Conduit}

Conduits are an advanced form of ``pipes'' using arrows, a generalisation of monads~\cite{ConduitRef}.
\emph{Conduit} is a Clojure library developed by Duey and 
contributors~\footnote{Github home of Conduit: \url{https://github.com/jduey/conduit}} 
that supports programming with conduits.

The library had to be modified and simplified considerably to be able to type check.
Many existing conduits had troublesome variable-parameters, reminiscent of the
issues of assigning types to \lstinline|assoc| and \lstinline|partial|
(which is future work, see Section \ref{sec:futurearity}).
In these cases, simplified versions of the conduits were created that 
took fixed arguments.

An interesting property of the types assigned to conduits (which
are internally functions of a single argument) is that they are 
recursive types. This is not in itself interesting, but Typed Clojure
implements conduit types as \emph{type functions} with variance,
which required the variance of the conduit types to be known in advance.

Listing \ref{lst:conduittypes} shows the type constructor for conduit,
\lstinline|==>|. Notice the body of \lstinline|==>| includes a reference
to itself in a position where variance must be known in advance.
\lstinline|declare-alias-kind| is used to declare the kind of an alias.
Once an alias with an already declared kind is defined with \lstinline|def-alias|,
the defined kind must match the declared kind, otherwise a type error is thrown.
(The specifics of the types in Listing \ref{lst:conduittypes} are not relevant to the rest of this discussion,
so they are not explained).

\begin{lstlisting}[caption=Types for Conduits, label=lst:conduittypes]
...

(def-alias Result
  (TFn [[x :variance :covariant]]
    (U nil ;stream is closed
       '[] ;abort/skip
       '[x];consume/continue
       )))

(def-alias Cont
  (TFn [[in :variance :covariant]
        [out :variance :invariant]]
    [(U nil [(Result in) -> (Result out)]) -> (Result out)]))


(declare-alias-kind ==> (TFn [[in :variance :contravariant]
                              [out :variance :invariant]] Any))

(def-alias ==>
  (TFn [[in :variance :contravariant] 
        [out :variance :invariant]]
    [in -> '[(U nil (==> in out)) (Cont out out)]]))

...
\end{lstlisting}


The resulting port of \emph{Conduit} was unsatisfying for real world use.
The simplifications made in order to type check the library reversed
any effort that was invested into \emph{Conduit} to conform to common Clojure idioms.
The future work on improving variable-argument types should reveal whether
this library can be ported satisfactorily. It is desirable to port
this library because implementations and usages of conduits result in very abstract code
which static type systems can help verify as correct.
